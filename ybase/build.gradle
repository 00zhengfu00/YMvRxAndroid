apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/MANIFEST'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
//使用Kotlin实验特性
    androidExtensions {
        experimental = true
    }
    kapt {
        correctErrorTypes = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dataBinding {
        enabled = true
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}
dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api rootProject.ext.dependencies.kotlin_jdk
    api rootProject.ext.dependencies.appcompat_v7
    api rootProject.ext.dependencies.core_ktx
    api rootProject.ext.dependencies.constraintLayout
    api rootProject.ext.dependencies.navigation_fragment_ktx
    api rootProject.ext.dependencies.navigation_ui_ktx
    api rootProject.ext.dependencies.fragment
    api rootProject.ext.dependencies.fragment_ktx
    api rootProject.ext.dependencies.utilcode
    api rootProject.ext.dependencies.okhttp3
    api rootProject.ext.dependencies.okhttp3_log
    api rootProject.ext.dependencies.retrofit
    api rootProject.ext.dependencies.retrofit_converter_gson
    api rootProject.ext.dependencies.glide
    api rootProject.ext.dependencies.glide_okhttp
    api rootProject.ext.dependencies.multidex
    api rootProject.ext.dependencies.gson
    api rootProject.ext.dependencies.autosize
    api rootProject.ext.dependencies.epoxy
    kapt rootProject.ext.dependencies.epoxyProcessor
    api rootProject.ext.dependencies.coroutines_android
    api rootProject.ext.dependencies.lifecycle_viewmodel_ktx
    api rootProject.ext.dependencies.lifecycle_livedata_ktx
    api rootProject.ext.dependencies.lifecycle_extensions

//    // Koin
//    def koin_version = "2.1.4"
//    implementation "org.koin:koin-android:$koin_version"
//    implementation "org.koin:koin-androidx-scope:$koin_version"
//    implementation "org.koin:koin-androidx-viewmodel:$koin_version"


}
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "2.0.1"

// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/Yinzeyu/YMvRxAndroid'
// 项目主页。
def gitUrl = 'https://github.com/Yinzeyu/YMvRxAndroid.git'
// Git仓库的url。

group = "com.yzy"
install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'BaseLib For Android' // 可选，项目名称。
                description 'BaseLib' // 可选，项目描述。
                url siteUrl // 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'yinzeyu' // 开发者的id。
                        name 'yinzeyu' // 开发者名字。
                        email 'yin974415243@gmail.com' // 开发者邮箱。
                    }
                }

                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "maven"
        // 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字，如何创建请看下图。）
        name = "YBase"
        userOrg = 'yuge' // Bintray的用户名，2016年11月更新。
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}